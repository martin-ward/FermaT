C:"
==========================================================================
FermaT Transformation System
Copyright (C) 2001 Software Migrations Limited.
Email: martin@gkc.org.uk

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
==========================================================================";

C:" NB Some of these patterns are only worth doing for an explicit Simplify request: ";
C:" so maybe check for Budget > 5 (say). ";

MW_PROC @Mth_Patterns(Budget) ==

  FOREACH Expression DO

    IFMATCH Expression (-(- ~?x))
    THEN @Paste_Over(x) ENDMATCH;

    IFMATCH Expression -(~?x + ~*yy)
    THEN @Paste_Over(FILL Expression (- ~?x) - (~*yy) ENDFILL) ENDMATCH;

    IFMATCH Expression -(~?x MOD ~?y)
    THEN @Paste_Over(FILL Expression (- ~?x) MOD ~?y ENDFILL) ENDMATCH;

    IFMATCH Expression -(~?x DIV ~?y)
    THEN @Paste_Over(FILL Expression (- ~?x) DIV ~?y ENDFILL) ENDMATCH;

    IFMATCH Expression 1/(1/(~?x))
    THEN @Paste_Over(x) ENDMATCH;

    IFMATCH Expression ABS(~?x)
    THEN IF @ST(@Simplify(FILL Condition ~?x >= 0 ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(x) FI ENDMATCH;

    IFMATCH Expression ABS(~?x) * ABS(~?y)
    THEN @Paste_Over(FILL Expression ABS(~?x * ~?y) ENDFILL) ENDMATCH;

    IFMATCH Expression ABS(- ~?x)
    THEN IF @ST(@Simplify(FILL Condition ~?x <= 0 ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(FILL Expression (- ~?x) ENDFILL) FI ENDMATCH;

    IFMATCH Expression ABS(- ~?x)
    THEN @Paste_Over(FILL Expression ABS(~?x) ENDFILL) ENDMATCH;

    IFMATCH Expression ABS(FRAC(~?x))
    THEN @Paste_Over(FILL Expression FRAC(ABS(~?x)) ENDFILL) ENDMATCH;

    IFMATCH Expression SGN(~?x)
    THEN IF @ST(@Simplify(FILL Condition ~?x < 0 ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(Mth_m1)
	 ELSIF @ST(@Simplify(FILL Condition ~?x = 0 ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(Mth_0)
	 ELSIF @ST(@Simplify(FILL Condition ~?x > 0 ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(Mth_1) FI ENDMATCH;


    IFMATCH Expression ~?x MOD ~?=x
    THEN @Paste_Over(Mth_0) ENDMATCH;

    IFMATCH Expression 0 MOD ~?x
    THEN @Paste_Over(Mth_0) ENDMATCH;

    IFMATCH Expression ~?x MOD ~?y
    THEN IF @ST(@Simplify(FILL Condition ~?x < ~?y ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(Mth_0) FI ENDMATCH;

    IFMATCH Expression ~?x DIV ~?=x
    THEN @Paste_Over(Mth_1) ENDMATCH;

    IFMATCH Expression 0 DIV ~?x
    THEN @Paste_Over(Mth_0) ENDMATCH;

    IFMATCH Expression ~?x DIV 1
    THEN @Paste_Over(x) ENDMATCH;

    IFMATCH Expression ~?x DIV ~?y
    THEN IF @ST(@Simplify(FILL Condition ~?x < ~?y ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(Mth_0) FI ENDMATCH;

    IFMATCH Expression (- ~?x) DIV (- ~?y)
    THEN @Paste_Over(FILL Expression ~?x DIV ~?y ENDFILL) ENDMATCH;

    IFMATCH Expression IF TRUE THEN ~?x ELSE ~?y FI
    THEN @Paste_Over(x) ENDMATCH;

    IFMATCH Expression IF FALSE THEN ~?x ELSE ~?y FI
    THEN @Paste_Over(y) ENDMATCH;

    IFMATCH Expression IF ~?x THEN ~?y ELSE ~?=y FI
    THEN @Paste_Over(y) ENDMATCH;

    IFMATCH Expression (- ~?x) + ~?=x
    THEN @Paste_Over(Mth_0) ENDMATCH;

    IFMATCH Expression ~*y1 + (- ~?x) + ~?=x + ~*y2
    THEN @Paste_Over(@Make(T_Plus, < >, <Mth_0> ++ y1 ++ y2)) ENDMATCH;

    IFMATCH Expression ~*y1 + (- ~?x) + ~?yy + ~?=x + ~*y2
    THEN @Paste_Over(@Make(T_Plus, < >, y1 ++ <yy> ++ y2)) ENDMATCH;

    IFMATCH Expression (- ~?x) * ~?=x
    THEN @Paste_Over(FILL Expression -(~?x ** 2) ENDFILL) ENDMATCH;

    IFMATCH Expression ~*y1 * (- ~?x) * ~?=x * ~*y2
    THEN @Paste_Over(@Make(T_Times, < >,
			   <FILL Expression -(~?x ** 2) ENDFILL> ++ y1 ++ y2)) ENDMATCH;

    IFMATCH Expression ~*y1 * (- ~?x) * ~?yy * ~?=x * ~*y2
    THEN @Paste_Over(@Make(T_Times, < >,
			   <FILL Expression -(~?x ** 2) ENDFILL>
			     ++ y1 ++ <yy> ++ y2)) ENDMATCH;

    IFMATCH Expression (1/~?x) * ~?=x
    THEN @Paste_Over(Mth_1) ENDMATCH;

    IFMATCH Expression ~?x * ~?y * ~?z
    THEN IF @ST(x) = T_Invert AND @Equal?(x^1, z)
           THEN @Paste_Over(y) FI ENDMATCH;

    IFMATCH Expression ~*z1 * ~?x * ~?y
    THEN IF @ST(y) = T_Invert
           THEN IF @ST(x) = T_Number AND @ST(y^1) = T_Number AND @V(y^1) > 0
                   AND @V(y^1) MOD @V(x) = 0
                  THEN y := @Make(T_Number, @V(y^1) DIV @V(x), < >);
                       IF @V(y) = 1
                         THEN @Paste_Over(FILL Expression ~*z1 * 1 ENDFILL)
                         ELSE @Paste_Over(FILL Expression ~*z1 * 1/~?y
			                  ENDFILL) FI FI FI ENDMATCH;

    IFMATCH Expression ~?x ** 0
    THEN @Paste_Over(Mth_1) ENDMATCH;

    IFMATCH Expression ~?x ** 1
    THEN @Paste_Over(x) ENDMATCH;

    IFMATCH Expression 0 ** ~?x
    THEN @Paste_Over(Mth_0) ENDMATCH;

    IFMATCH Expression ~?x ** (- ~?y)
    THEN @Paste_Over(FILL Expression 1/(~?x ** ~?y) ENDFILL) ENDMATCH;

    IFMATCH Expression MAX(~?x, ~?y)
    THEN IF @ST(@Simplify(FILL Condition ~?x >= ~?y ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(x) FI ENDMATCH;

    IFMATCH Expression MAX(~?x, ~?y)
    THEN IF @ST(@Simplify(FILL Condition ~?y >= ~?x ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(y) FI ENDMATCH;

    IFMATCH Expression MIN(~?x, ~?y)
    THEN IF @ST(@Simplify(FILL Condition ~?x <= ~?y ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(x) FI ENDMATCH;

    IFMATCH Expression MIN(~?x, ~?y)
    THEN IF @ST(@Simplify(FILL Condition ~?y <= ~?x ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(y) FI ENDMATCH;

    IFMATCH Expression (~?x * ~?y) DIV ~?=x
    THEN @Paste_Over(y) ENDMATCH;

    IFMATCH Expression (~?y * ~?x) DIV ~?=x
    THEN @Paste_Over(y) ENDMATCH;

    IFMATCH Expression (~?x * ~?y) DIV ~?z
    THEN IF @ST(y) = T_Number AND @ST(z) = T_Number AND -@V(y) = @V(z)
           THEN @Paste_Over(FILL Expression -~?x ENDFILL) FI ENDMATCH;

    IFMATCH Expression (~?y * ~?x + ~?z) DIV ~?=x
    THEN @Paste_Over(FILL Expression ~?y + (~?z DIV ~?x) ENDFILL) ENDMATCH;

    IFMATCH Expression (~?z + ~?y * ~?x) DIV ~?=x
    THEN @Paste_Over(FILL Expression ~?y + (~?z DIV ~?x) ENDFILL) ENDMATCH;

    IFMATCH Expression (~?x * ~?y + ~?z) DIV ~?=x
    THEN @Paste_Over(FILL Expression ~?y + (~?z DIV ~?x) ENDFILL) ENDMATCH;

    IFMATCH Expression (~?z + ~?x * ~?y) DIV ~?=x
    THEN @Paste_Over(FILL Expression ~?y + (~?z DIV ~?x) ENDFILL) ENDMATCH;

    IFMATCH Expression IF TRUE THEN ~?x ELSE ~?y FI
    THEN @Paste_Over(x) ENDMATCH;

    IFMATCH Expression IF FALSE THEN ~?x ELSE ~?y FI
    THEN @Paste_Over(y) ENDMATCH;

    IFMATCH Expression ((- ~?y) * 2) + ~*z + ~?=y + ~*w
    THEN @Paste_Over(FILL Expression ~*z + ~*w - ~?y ENDFILL) ENDMATCH;

    IFMATCH Expression HEAD(<~?x, ~*y>)
    THEN @Paste_Over(x) ENDMATCH;

    IFMATCH Expression HEAD(<~?x, ~*y> ++ ~*z)
    THEN @Paste_Over(x) ENDMATCH;

    IFMATCH Expression TAIL(<~?x, ~*y>)
    THEN @Paste_Over(@Make(T_Sequence, < >, <@Make(T_Expressions, < >, y)>)) ENDMATCH;

    IFMATCH Expression TAIL(<~?x, ~*y> ++ ~*z)
    THEN IF EMPTY?(y)
           THEN IF LENGTH(z) > 1
	          THEN @Paste_Over(@Make(T_Concat, < >, z))
		  ELSE @Paste_Over(HEAD(z)) FI
           ELSE y := @Make(T_Sequence, < >, <@Make(T_Expressions, < >, y)>);
	        @Paste_Over(@Make(T_Concat, < >, <y> ++ z)) FI ENDMATCH;

    IFMATCH Expression < > ++ ~?x
      THEN @Paste_Over(x) ENDMATCH;

    IFMATCH Expression < ~*x > ++ < ~*y >
      THEN @Paste_Over(FILL Expression < ~*x, ~*y >) ENDMATCH;

    IFMATCH Expression LENGTH(< ~*x >)
      THEN @Paste_Over(@Make(T_Number, LENGTH(x), < >)) ENDMATCH;

    IFMATCH Expression !XF srl(2 ** ~?x * ~?y, ~?=x)
      THEN @Paste_Over(y) ENDMATCH;

    IFMATCH Expression @List_To_String(<~*x>)
      THEN VAR < OK := 1 >:
           FOR elt IN x DO
             IF @ST(elt) <> T_Number OR @V(elt) = 34 OR @V(elt) = 13
	       THEN OK := 0 FI OD;
           IF OK = 1
             THEN @Paste_Over(@Make(T_String,
	                            @List_To_String(MAP("@V", x)),
				    < >)) FI ENDVAR ENDMATCH OD;


  FOREACH Condition DO

    IFMATCH Condition ~?x = ~?=x
    THEN @Paste_Over(Mth_True) ENDMATCH;

    IFMATCH Condition ~?x <> ~?=x
    THEN @Paste_Over(Mth_False) ENDMATCH;

    IFMATCH Condition ~?x < ~?=x
    THEN @Paste_Over(Mth_False) ENDMATCH;

    IFMATCH Condition ~?x <= ~?=x
    THEN @Paste_Over(Mth_True) ENDMATCH;

    IFMATCH Condition (- ~?x) = 0
    THEN @Paste_Over(FILL Condition ~?x = 0 ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x + (- ~?y) = 0
    THEN @Paste_Over(FILL Condition ~?x = ~?y ENDFILL) ENDMATCH;

    IFMATCH Condition (- ~?y) + ~*x = 0
    THEN @Paste_Over(FILL Condition 0 + ~*x = ~?y ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x + ~?y = 0
    THEN @Paste_Over(FILL Condition ~?x = - ~?y ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x * ~?y = 0
    THEN @Paste_Over(FILL Condition ~?x = 0 OR ~?y = 0 ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x ** ~?y = 0
    THEN @Paste_Over(FILL Condition ~?x = 0 ENDFILL) ENDMATCH;

    IFMATCH Condition 1/~?x = 0
    THEN @Paste_Over(Mth_False) ENDMATCH;

    IFMATCH Condition ~?x MOD 2 = 0
    THEN @Paste_Over(FILL Condition EVEN?(~?x) ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x MOD 2 = 1
    THEN @Paste_Over(FILL Condition ODD?(~?x) ENDFILL) ENDMATCH;

    IFMATCH Condition ABS(~?x) = 0
    THEN @Paste_Over(FILL Condition ~?x = 0 ENDFILL) ENDMATCH;

    IFMATCH Condition SGN(~?x) = 0
    THEN @Paste_Over(FILL Condition ~?x = 0 ENDFILL) ENDMATCH;

    IFMATCH Condition SGN(~?x) = 1
    THEN @Paste_Over(FILL Condition ~?x > 0 ENDFILL) ENDMATCH;

    IFMATCH Condition SGN(~?x) = -1
    THEN @Paste_Over(FILL Condition ~?x < 0 ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x + ~?y = ~?=x + ~?z
    THEN @Paste_Over(FILL Condition ~?y = ~?z ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x + ~?y = ~?z + ~?=y
    THEN @Paste_Over(FILL Condition ~?x = ~?z ENDFILL) ENDMATCH;


    IFMATCH Condition (- ~?x) <> 0
    THEN @Paste_Over(FILL Condition ~?x <> 0 ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x + (- ~?y) <> 0
    THEN @Paste_Over(FILL Condition ~?x <> ~?y ENDFILL) ENDMATCH;

    IFMATCH Condition (- ~?y) + ~?x <> 0
    THEN @Paste_Over(FILL Condition ~?x <> ~?y ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x * ~?y <> 0
    THEN @Paste_Over(FILL Condition ~?x <> 0 AND ~?y <> 0 ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x ** ~?y <> 0
    THEN @Paste_Over(FILL Condition ~?x <> 0 ENDFILL) ENDMATCH;

    IFMATCH Condition 1/~?x <> 0
    THEN @Paste_Over(Mth_True) ENDMATCH;

    IFMATCH Condition ~?x MOD 2 <> 0
    THEN @Paste_Over(FILL Condition ODD?(~?x) ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x MOD 2 <> 1
    THEN @Paste_Over(FILL Condition EVEN?(~?x) ENDFILL) ENDMATCH;

    IFMATCH Condition ABS(~?x) <> 0
    THEN @Paste_Over(FILL Condition ~?x <> 0 ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x + ~?y <> ~?=x + ~?z
    THEN @Paste_Over(FILL Condition ~?y <> ~?z ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x + ~?y <> ~?z + ~?=y
    THEN @Paste_Over(FILL Condition ~?x <> ~?z ENDFILL) ENDMATCH;


    IFMATCH Condition (- ~?x) < 0
    THEN @Paste_Over(FILL Condition 0 < ~?x ENDFILL) ENDMATCH;

    IFMATCH Condition (- ~?x) <= 0
    THEN @Paste_Over(FILL Condition 0 <= ~?x ENDFILL) ENDMATCH;

    IFMATCH Condition 0 < (- ~?x)
    THEN @Paste_Over(FILL Condition ~?x < 0 ENDFILL) ENDMATCH;

    IFMATCH Condition 0 <= (- ~?x)
    THEN @Paste_Over(FILL Condition ~?x <= 0 ENDFILL) ENDMATCH;

    IFMATCH Condition (- ~?x) + ~?y < 0
    THEN @Paste_Over(FILL Condition ~?y < ~?x ENDFILL) ENDMATCH;

    IFMATCH Condition (- ~?x) + ~?y <= 0
    THEN @Paste_Over(FILL Condition ~?y <= ~?x ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x + (- ~?y) < 0
    THEN @Paste_Over(FILL Condition ~?x < ~?y ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x + (- ~?y) <= 0
    THEN @Paste_Over(FILL Condition ~?x <= ~?y ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x + ~?y < 0
    THEN @Paste_Over(FILL Condition ~?x < - ~?y ENDFILL) ENDMATCH;

    IFMATCH Condition 0 < ~?x + ~?y
    THEN @Paste_Over(FILL Condition - ~?y < ~?x ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x ** ~?y < 0
    THEN @Paste_Over(Mth_False) ENDMATCH;

    IFMATCH Condition ABS(~?x) < 0
    THEN @Paste_Over(Mth_False) ENDMATCH;

    IFMATCH Condition 0 < ABS(~?x)
    THEN @Paste_Over(FILL Condition ~?x <> 0 ENDFILL) ENDMATCH;

    IFMATCH Condition ABS(~?x) <= 0
    THEN @Paste_Over(FILL Condition ~?x = 0 ENDFILL) ENDMATCH;

    IFMATCH Condition 0 <= ABS(~?x)
    THEN @Paste_Over(Mth_True) ENDMATCH;

    IFMATCH Condition ABS(~?x) < ~?=x
    THEN @Paste_Over(Mth_False) ENDMATCH;

    IFMATCH Condition ABS(~?x) < ~?y
    THEN IF @ST(@Simplify(FILL Condition ~?y <= 0 ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(Mth_False) FI ENDMATCH;

    IFMATCH Condition ABS(~?x) <= ~?y
    THEN IF @ST(@Simplify(FILL Condition ~?y < 0 ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(Mth_False) FI ENDMATCH;

    IFMATCH Condition ~?x + ~*y < ~?=x + ~*z
    THEN @Paste_Over(FILL Condition 0 + ~*y < 0 + ~*z ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x + ~*y < ~?=x
    THEN @Paste_Over(FILL Condition 0 + ~*y < 0 ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x < ~?=x + ~*z
    THEN @Paste_Over(FILL Condition 0 < 0 + ~*z ENDFILL) ENDMATCH;

    IFMATCH Condition ~*x + ~?y < ~*z + ~?=y
    THEN @Paste_Over(FILL Condition 0 + ~*x < 0 + ~*z ENDFILL) ENDMATCH;

    IFMATCH Condition ~*x + ~?y < ~?=y
    THEN @Paste_Over(FILL Condition 0 + ~*x < 0 ENDFILL) ENDMATCH;

    IFMATCH Condition ~?y < ~*z + ~?=y
    THEN @Paste_Over(FILL Condition 0 < 0 + ~*z ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x + ~*y <= ~?=x + ~*z
    THEN @Paste_Over(FILL Condition 0 + ~*y <= 0 + ~*z ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x + ~*y <= ~?=x
    THEN @Paste_Over(FILL Condition 0 + ~*y <= 0 ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x <= ~?=x + ~*z
    THEN @Paste_Over(FILL Condition 0 <= 0 + ~*z ENDFILL) ENDMATCH;

    IFMATCH Condition ~*x + ~?y <= ~*z + ~?=y
    THEN @Paste_Over(FILL Condition 0 + ~*x <= 0 + ~*z ENDFILL) ENDMATCH;

    IFMATCH Condition ~?y <= ~*z + ~?=y
    THEN @Paste_Over(FILL Condition 0 <= 0 + ~*z ENDFILL) ENDMATCH;

    IFMATCH Condition ~*x + ~?y <= ~?=y
    THEN @Paste_Over(FILL Condition 0 + ~*x <= 0 ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x < ~?y + ~?z
    THEN IF @ST(x) = T_Number AND @ST(z) = T_Number
           THEN @Paste_Over(FILL Condition ~?x - ~?z < ~?y ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x + ~?y <= ~?z
    THEN IF @ST(y) = T_Number AND @ST(z) = T_Number
           THEN @Paste_Over(FILL Condition ~?x <= ~?z - ~?y ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x + ~?y <> ~?z
    THEN IF @ST(y) = T_Number AND @ST(z) = T_Number
           THEN @Paste_Over(FILL Condition ~?x <> ~?z - ~?y ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x <= ~?y + ~?z
    THEN IF @ST(x) = T_Number AND @ST(z) = T_Number
           THEN @Paste_Over(FILL Condition ~?x - ~?z <= ~?y ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x + ~?y = ~?z
    THEN IF @ST(y) = T_Number AND @ST(z) = T_Number
           THEN @Paste_Over(FILL Condition ~?x = ~?z - ~?y ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x * ~?y = ~?z
    THEN IF @ST(y) = T_Number AND @ST(z) = T_Number AND @V(y) <> 0
              AND @V(z) MOD @V(y) = 0
           THEN @Paste_Over(FILL Condition ~?x = ~?z DIV ~?y ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?z <= ~?x * ~?y
    THEN IF @ST(y) = T_Number AND @ST(z) = T_Number AND @V(y) <> 0
              AND @V(z) MOD @V(y) = 0
           THEN @Paste_Over(FILL Condition ~?z DIV ~?y <= ~?x ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?z < ~?x * ~?y
    THEN IF @ST(y) = T_Number AND @ST(z) = T_Number AND @V(y) <> 0
              AND @V(z) MOD @V(y) = 0
           THEN @Paste_Over(FILL Condition ~?z DIV ~?y < ~?x ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x * ~?y = ~?z
    THEN IF @ST(y) = T_Number AND @ST(z) = T_Number AND @V(y) <> 0
           THEN @Paste_Over(FILL Condition ~?x = ~?z / ~?y ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x < ~?y * ~?z
    THEN IF @ST(z) = T_Invert
           THEN VAR < z1 := z^1 >:
                @Paste_Over(FILL Condition ~?x * ~?z1 < ~?y ENDFILL) ENDVAR FI ENDMATCH;

    IFMATCH Condition ~?x * ~?y < ~?=x * ~?z
    THEN IF @ST(@Simplify(FILL Condition ~?x > 0 ENDFILL, Budget - 1)) = T_True
           THEN @Paste_Over(FILL Condition ~?y < ~?z ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x * ~?y <= ~?=x * ~?z
    THEN IF @ST(@Simplify(FILL Condition ~?x > 0 ENDFILL, Budget - 1)) = T_True
           THEN @Paste_Over(FILL Condition ~?y <= ~?z ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x * ~?y > ~?=x * ~?z
    THEN IF @ST(@Simplify(FILL Condition ~?x > 0 ENDFILL, Budget - 1)) = T_True
           THEN @Paste_Over(FILL Condition ~?y > ~?z ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x * ~?y >= ~?=x * ~?z
    THEN IF @ST(@Simplify(FILL Condition ~?x > 0 ENDFILL, Budget - 1)) = T_True
           THEN @Paste_Over(FILL Condition ~?y >= ~?z ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x * ~?y < ~?z * ~?=y
    THEN IF @ST(@Simplify(FILL Condition ~?y > 0 ENDFILL, Budget - 1)) = T_True
           THEN @Paste_Over(FILL Condition ~?x < ~?z ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x * ~?y <= ~?z * ~?=y
    THEN IF @ST(@Simplify(FILL Condition ~?y > 0 ENDFILL, Budget - 1)) = T_True
           THEN @Paste_Over(FILL Condition ~?x <= ~?z ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x * ~?y > ~?z * ~?=y
    THEN IF @ST(@Simplify(FILL Condition ~?y > 0 ENDFILL, Budget - 1)) = T_True
           THEN @Paste_Over(FILL Condition ~?x > ~?z ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x * ~?y >= ~?z * ~?=y
    THEN IF @ST(@Simplify(FILL Condition ~?y > 0 ENDFILL, Budget - 1)) = T_True
           THEN @Paste_Over(FILL Condition ~?x >= ~?z ENDFILL) FI ENDMATCH;

    IFMATCH Condition -~?x < -~?y
    THEN @Paste_Over(FILL Condition ~?y < ~?x ENDFILL) ENDMATCH;

    IFMATCH Condition -~?x <= -~?y
    THEN @Paste_Over(FILL Condition ~?y <= ~?x ENDFILL) ENDMATCH;

    IFMATCH Condition -~?x = -~?y
    THEN @Paste_Over(FILL Condition ~?x = ~?y ENDFILL) ENDMATCH;

    IFMATCH Condition -~?x = ~?y
    THEN IF @ST(y) = T_Number
           THEN y := @Make(T_Number, -@V(y), < >);
                @Paste_Over(FILL Condition ~?x = ~?y ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x < -~?y
    THEN IF @ST(x) = T_Number
           THEN @Paste_Over(FILL Condition ~?y < -~?x ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x + (-~?y) = ~?z
    THEN IF @ST(z) = T_Number AND @V(z) < 0
           THEN z := @Make(T_Number, -@V(z), < >);
	        @Paste_Over(FILL Condition ~?x + ~?z = ~?y ENDFILL) FI ENDMATCH;

    IFMATCH Condition (-~?x) + ~?y = ~?z
    THEN IF @ST(z) = T_Number AND @V(z) < 0
           THEN z := @Make(T_Number, -@V(z), < >);
	        @Paste_Over(FILL Condition ~?y + ~?z = ~?x ENDFILL) FI ENDMATCH;

    IFMATCH Condition -~?x = (-~?y) + ~?z
    THEN @Paste_Over(FILL Condition ~?x = ~?y - ~?z ENDFILL) ENDMATCH;

    IFMATCH Condition (-~?y) + ~?z = -~?x
    THEN @Paste_Over(FILL Condition ~?x = ~?y - ~?z ENDFILL) ENDMATCH;

    IFMATCH Condition (~*y AND ~?x AND ~*z) OR ~?=x
    THEN @Paste_Over(x) ENDMATCH;

    IFMATCH Condition ~?x OR (~*y AND ~?=x AND ~*z)
    THEN @Paste_Over(x) ENDMATCH;

    IFMATCH Condition ~?y1 = ~?y2 AND ~?x = ~?z AND ~?=x <> ~?w AND ~*yy
    THEN IF @Mth_Constant?(z) AND @Mth_Constant?(w) AND NOT @Equal?(z, w)
	   THEN @Paste_Over(@Make(T_And, < >,
				  <FILL Condition ~?y1 = ~?y2 ENDFILL,
				   FILL Condition ~?x = ~?z ENDFILL> ++ yy)) FI ENDMATCH;

    IFMATCH Condition ~?x = ~?z AND ~?=x <> ~?w AND ~*yy
    THEN IF @Mth_Constant?(z) AND @Mth_Constant?(w) AND NOT @Equal?(z, w)
	   THEN @Paste_Over(@Make(T_And, < >,
				  <FILL Condition ~?x = ~?z ENDFILL> ++ yy)) FI ENDMATCH;

    IFMATCH Condition ~?y1 = ~?y2 AND ~?x = ~?z AND ~?=x = ~?w AND ~*yy
    THEN IF @Mth_Constant?(z) AND @Mth_Constant?(w) AND NOT @Equal?(z, w)
	   THEN @Paste_Over(Mth_False) FI ENDMATCH;

    IFMATCH Condition ~?x = ~?z AND ~?=x = ~?w AND ~*yy
    THEN IF @Mth_Constant?(z) AND @Mth_Constant?(w) AND NOT @Equal?(z, w)
	   THEN @Paste_Over(Mth_False) FI ENDMATCH;

    C:" A bug in IFMATCH means that we can't match an empty yy in the above. ";
    C:" Hence these patterns are also needed: ";

    IFMATCH Condition ~?y1 = ~?y2 AND ~?x = ~?z AND ~?=x <> ~?w
    THEN IF @Mth_Constant?(z) AND @Mth_Constant?(w) AND NOT @Equal?(z, w)
	   THEN @Paste_Over(@Make(T_And, < >,
				  <FILL Condition ~?y1 = ~?y2 ENDFILL,
				   FILL Condition ~?x = ~?z ENDFILL>)) FI ENDMATCH;

    IFMATCH Condition ~?x = ~?z AND ~?=x <> ~?w
    THEN IF @Mth_Constant?(z) AND @Mth_Constant?(w) AND NOT @Equal?(z, w)
	   THEN @Paste_Over(@Make(T_And, < >,
				  <FILL Condition ~?x = ~?z ENDFILL>)) FI ENDMATCH;

    IFMATCH Condition ~?y1 = ~?y2 AND ~?x = ~?z AND ~?=x = ~?w
    THEN IF @Mth_Constant?(z) AND @Mth_Constant?(w) AND NOT @Equal?(z, w)
	   THEN @Paste_Over(Mth_False) FI ENDMATCH;

    IFMATCH Condition ~?x = ~?z AND ~?=x = ~?w
    THEN IF @Mth_Constant?(z) AND @Mth_Constant?(w) AND NOT @Equal?(z, w)
	   THEN @Paste_Over(Mth_False) FI ENDMATCH;


    IFMATCH Condition MAX(~?x, ~?z) < ~?y
    THEN @Paste_Over(FILL Condition (~?x < ~?y) AND (~?z < ~?y) ENDFILL) ENDMATCH;

    IFMATCH Condition MAX(~?x, ~?z) <= ~?y
    THEN @Paste_Over(FILL Condition (~?x <= ~?y) AND (~?z <= ~?y) ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x < MIN(~?y, ~?z)
    THEN @Paste_Over(FILL Condition (~?x < ~?y) AND (~?x < ~?z) ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x <= MIN(~?y, ~?z)
    THEN @Paste_Over(FILL Condition (~?x <= ~?y) AND (~?x <= ~?z) ENDFILL) ENDMATCH;

    IFMATCH Condition MIN(~?x, ~?z) < ~?y
    THEN @Paste_Over(FILL Condition (~?x < ~?y) OR (~?z < ~?y) ENDFILL) ENDMATCH;

    IFMATCH Condition MIN(~?x, ~?z) <= ~?y
    THEN @Paste_Over(FILL Condition (~?x <= ~?y) OR (~?z <= ~?y) ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x < MAX(~?y, ~?z)
    THEN @Paste_Over(FILL Condition (~?x < ~?y) OR (~?x < ~?z) ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x <= MAX(~?y, ~?z)
    THEN @Paste_Over(FILL Condition (~?x <= ~?y) OR (~?x <= ~?z) ENDFILL) ENDMATCH;


    IFMATCH Condition ~?x = ~?y OR ~?=x <> ~?=y
    THEN @Paste_Over(Mth_True) ENDMATCH;

    IFMATCH Condition ~?x = ~?y AND ~?=x <> ~?=y
    THEN @Paste_Over(Mth_False) ENDMATCH;

    IFMATCH Condition ~?x = ~?y AND ~?=x <> ~?=y AND ~?z
    THEN @Paste_Over(Mth_False) ENDMATCH;

    IFMATCH Condition ~?z AND ~?x = ~?y AND ~?=x <> ~?=y
    THEN @Paste_Over(Mth_False) ENDMATCH;

    IFMATCH Condition ~?x = ~?y OR ~?=x < ~?=y
    THEN @Paste_Over(FILL Condition ~?x <= ~?y ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x = ~?y OR ~?=y < ~?=x
    THEN @Paste_Over(FILL Condition ~?y <= ~?x ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x <> ~?y AND ~?=x <= ~?=y
    THEN @Paste_Over(FILL Condition ~?x < ~?y ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x <> ~?y AND ~?=y <= ~?=x
    THEN @Paste_Over(FILL Condition ~?y < ~?x ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x <> ~?y AND ~?=x < ~?=y
    THEN @Paste_Over(FILL Condition ~?x < ~?y ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x <> ~?y AND ~?=y < ~?=x
    THEN @Paste_Over(FILL Condition ~?y < ~?x ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x < ~?y OR ~?=x <= ~?=y
    THEN @Paste_Over(FILL Condition ~?x <= ~?y ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x < ~?y OR ~?=y <= ~?=x
    THEN @Paste_Over(FILL Condition ~?y <= ~?x ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x < ~?y OR ~?=y <= ~?z
    THEN IF @ST(@Simplify(FILL Condition ~?x <= ~?z ENDFILL, Budget - 1)) = T_True
           THEN @Paste_Over(Mth_True) FI ENDMATCH;

    IFMATCH Condition ~?x < ~?y AND ~?=x <= ~?=y
    THEN @Paste_Over(FILL Condition ~?x < ~?y ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x < ~?y AND ~?=y <= ~?=x
    THEN @Paste_Over(Mth_False) ENDMATCH;

    IFMATCH Condition ~?x < ~?y AND ~?=y < ~?=x
    THEN @Paste_Over(Mth_False) ENDMATCH;

    IFMATCH Condition ~?x <= ~?y AND ~?=y <= ~?=x
    THEN @Paste_Over(FILL Condition ~?x = ~?y ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x < ~?y AND ~?=y < ~?z
    THEN IF @ST(@Simplify(FILL Condition ~?x < ~?z ENDFILL, Budget - 1)) = T_False
           THEN @Paste_Over(Mth_False) FI ENDMATCH;

    IFMATCH Condition ~?x < ~?y AND ~?z < ~?=x
    THEN IF @ST(@Simplify(FILL Condition ~?z < ~?y ENDFILL, Budget - 1)) = T_False
           THEN @Paste_Over(Mth_False) FI ENDMATCH;

    IFMATCH Condition ~?x < ~?y AND ~?=y <= ~?z
    THEN IF @ST(@Simplify(FILL Condition ~?x < ~?z ENDFILL, Budget - 1)) = T_False
           THEN @Paste_Over(Mth_False) FI ENDMATCH;

    IFMATCH Condition ~?x < ~?y AND ~?z <= ~?=y
    THEN IF @ST(@Simplify(FILL Condition ~?z < ~?x ENDFILL, Budget - 1)) = T_True
           THEN @Paste_Over(FILL Condition ~?x < ~?y ENDFILL) FI ENDMATCH;



    IFMATCH Condition ~?x = ~?y OR ~?=x < ~?z
    THEN IF @ST(@Simplify(FILL Condition ~?y < ~?z ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(FILL Condition ~?x < ~?z ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x = ~?y OR ~?z < ~?=x
    THEN IF @ST(@Simplify(FILL Condition ~?y > ~?z ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(FILL Condition ~?z < ~?x ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x = ~?y OR ~?=y < ~?z
    THEN IF @ST(@Simplify(FILL Condition ~?x < ~?z ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(FILL Condition ~?y < ~?z ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x = ~?y OR ~?z < ~?=y
    THEN IF @ST(@Simplify(FILL Condition ~?x > ~?z ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(FILL Condition ~?z < ~?y ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x = ~?y OR ~?=x <= ~?z
    THEN IF @ST(@Simplify(FILL Condition ~?y <= ~?z ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(FILL Condition ~?x <= ~?z ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x = ~?y OR ~?z <= ~?=x
    THEN IF @ST(@Simplify(FILL Condition ~?y >= ~?z ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(FILL Condition ~?z <= ~?x ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x = ~?y OR ~?=y <= ~?z
    THEN IF @ST(@Simplify(FILL Condition ~?x <= ~?z ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(FILL Condition ~?y <= ~?z ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x = ~?y OR ~?z <= ~?=y
    THEN IF @ST(@Simplify(FILL Condition ~?x >= ~?z ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(FILL Condition ~?z <= ~?y ENDFILL) FI ENDMATCH;


    IFMATCH Condition ~?x <> ~?y OR ~?=x < ~?z
    THEN IF @ST(@Simplify(FILL Condition ~?z > ~?y ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(Mth_True) FI ENDMATCH;

    IFMATCH Condition ~?x <> ~?y OR ~?z < ~?=x
    THEN IF @ST(@Simplify(FILL Condition ~?z > ~?y ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(Mth_True) FI ENDMATCH;

    IFMATCH Condition ~?x <> ~?y OR ~?=y < ~?z
    THEN IF @ST(@Simplify(FILL Condition ~?x > ~?z ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(Mth_True) FI ENDMATCH;

    IFMATCH Condition ~?x <> ~?y OR ~?z < ~?=y
    THEN IF @ST(@Simplify(FILL Condition ~?x > ~?z ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(Mth_True) FI ENDMATCH;

    IFMATCH Condition ~?x <> ~?y OR ~?=x < ~?z
    THEN IF @ST(@Simplify(FILL Condition ~?z <= ~?y ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(FILL Condition ~?x <> ~?y ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x <> ~?y OR ~?z < ~?=x
    THEN IF @ST(@Simplify(FILL Condition ~?y <= ~?z ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(FILL Condition ~?x <> ~?y ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x <> ~?y OR ~?=y < ~?z
    THEN IF @ST(@Simplify(FILL Condition ~?z <= ~?x ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(FILL Condition ~?x <> ~?y ENDFILL) FI ENDMATCH;

    IFMATCH Condition ~?x <> ~?y OR ~?z < ~?=y
    THEN IF @ST(@Simplify(FILL Condition ~?x <= ~?z ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(FILL Condition ~?x <> ~?y ENDFILL) FI ENDMATCH;


    IFMATCH Condition ~?x < ~?y OR ~?=y <= ~?=x
    THEN @Paste_Over(Mth_True) ENDMATCH;

    IFMATCH Condition ~?x < ~?y OR ~?=y < ~?=x
    THEN @Paste_Over(FILL Condition ~?x <> ~?y ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x < ~?y OR ~?z < ~?=x
    THEN IF @ST(@Simplify(FILL Condition ~?y > ~?z ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(Mth_True) FI ENDMATCH;

    IFMATCH Condition ~?x < ~?y OR ~?z <= ~?=x
    THEN IF @ST(@Simplify(FILL Condition ~?y >= ~?z ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(Mth_True) FI ENDMATCH;

    IFMATCH Condition ~?x <= ~?y OR ~?z <= ~?=x
    THEN IF @ST(@Simplify(FILL Condition ~?y >= ~?z ENDFILL, Budget - 1)) = T_True
	   THEN @Paste_Over(Mth_True) FI ENDMATCH;

    IFMATCH Condition ~?z < ~?x OR ~?=x <= ~?y OR ~?=y <= ~?=z
    THEN @Paste_Over(Mth_True) ENDMATCH;

    IFMATCH Condition ~?y < ~?x AND ~?z < ~?=y AND ~?=x <= ~?=z
    THEN @Paste_Over(Mth_False) ENDMATCH;

    IFMATCH Condition ~*yy OR ~?x = ~?y OR ~?=x < ~?=y
    THEN @Paste_Over(FILL Condition ~*yy OR ~?x <= ~?y ENDFILL) ENDMATCH;

    IFMATCH Condition ~*yy OR ~?y = ~?x OR ~?=x < ~?=y
    THEN @Paste_Over(FILL Condition ~*yy OR ~?x <= ~?y ENDFILL) ENDMATCH;

    IFMATCH Condition ~?n1 < ~?x AND ~?n2 < ~?=x AND ~*yy
    THEN IF @ST(n1) = T_Number AND @ST(n2) = T_Number AND @V(n2) > @V(n1)
           THEN @Paste_Over(FILL Condition  ~?n2 < ~?x AND ~*yy ENDFILL) FI ENDMATCH;


    IFMATCH Condition EVEN?(~?x) OR ODD?(~?=x)
    THEN @Paste_Over(Mth_True) ENDMATCH;

    IFMATCH Condition EVEN?(~?x) AND ODD?(~?=x)
    THEN @Paste_Over(Mth_False) ENDMATCH;

    IFMATCH Condition REDUCE("+", ~?x) < 0 AND 0 < ~?=x AND ~*yy
    THEN @Paste_Over(Mth_False) ENDMATCH;

    IFMATCH Condition 0 <= REDUCE("+", ~?x) AND 0 < ~?=x
    THEN @Paste_Over(FILL Condition 0 < ~?x ENDFILL) ENDMATCH;

    IFMATCH Condition ~?x AND ~?y
                        OR ~?=x AND ~?z AND ~?w
			OR ~?y1 AND ~?=z
      THEN IF @ST(@And(y, y1)) = T_False AND @ST(@Or(y, y1)) = T_True
             THEN @Paste_Over(FILL Condition ~?x AND ~?y
                                               OR ~?x AND ~?z
                                               OR ~?y1 AND ~?z ENDFILL) FI ENDMATCH;

    C:" Decimal patterns: ";

    IFMATCH Condition NOT !XC dec_less(~?x, ~?y)
      THEN @Paste_Over(FILL Condition !XC dec_greater_eq(~?x, ~?y) ENDFILL) ENDMATCH;

    IFMATCH Condition NOT !XC dec_less_eq(~?x, ~?y)
      THEN @Paste_Over(FILL Condition !XC dec_greater(~?x, ~?y) ENDFILL) ENDMATCH;

    IFMATCH Condition NOT !XC dec_greater(~?x, ~?y)
      THEN @Paste_Over(FILL Condition !XC dec_less_eq(~?x, ~?y) ENDFILL) ENDMATCH;

    IFMATCH Condition NOT !XC dec_greater_eq(~?x, ~?y)
      THEN @Paste_Over(FILL Condition !XC dec_less(~?x, ~?y) ENDFILL) ENDMATCH;

    IFMATCH Condition !XC dec_eq(~?x, ~?y) OR !XC dec_greater_eq(~?=x, ~?=y)
      THEN @Paste_Over(FILL Condition !XC dec_greater_eq(~?x, ~?y) ENDFILL) ENDMATCH;

    IFMATCH Condition !XC dec_eq(~?x, ~?y) OR !XC dec_less(~?=x, ~?=y)
      THEN @Paste_Over(FILL Condition !XC dec_less_eq(~?x, ~?y) ENDFILL) ENDMATCH;

    IFMATCH Condition !XC dec_greater_eq(~?x, ~?y) AND !XC dec_less_eq(~?=x, ~?=y)
      THEN @Paste_Over(FILL Condition !XC dec_eq(~?x, ~?y) ENDFILL) ENDMATCH;

    IFMATCH Condition NOT !XC dec_eq(~?x, ~?y) AND !XC dec_less(~?=x, ~?=y)
      THEN @Paste_Over(FILL Condition !XC dec_less(~?x, ~?y) ENDFILL) ENDMATCH;

    IFMATCH Condition NOT !XC dec_eq(~?x, ~?y) AND !XC dec_greater_eq(~?=x, ~?=y)
      THEN @Paste_Over(FILL Condition !XC dec_greater(~?x, ~?y) ENDFILL) ENDMATCH;

    IFMATCH Condition NOT !XC dec_eq(~?x, ~?y) AND !XC dec_eq(~?=x, ~?=y)
      THEN @Paste_Over(Mth_False) ENDMATCH;

    IFMATCH Condition NOT !XC dec_eq(~?x, ~?y) OR !XC dec_eq(~?=x, ~?=y)
      THEN @Paste_Over(Mth_True) ENDMATCH;

    IFMATCH Condition !XC dec_eq(~?x, ~?y) AND ~?z OR NOT !XC dec_eq(~?=x, ~?=y)
      THEN @Paste_Over(FILL Condition ~?z
                                        OR NOT !XC dec_eq(~?x, ~?y) ENDFILL) ENDMATCH;

    IFMATCH Condition !XC dec_greater_eq(~?x, ~?y) OR !XC dec_less(~?=x, ~?=y)
      THEN @Paste_Over(Mth_True) ENDMATCH;

    IFMATCH Condition !XC dec_less_eq(~?x, ~?y) OR !XC dec_greater(~?=x, ~?=y)
      THEN @Paste_Over(Mth_True) ENDMATCH;

    IFMATCH Condition !XC dec_greater_eq(~?x, ~?y) AND !XC dec_less(~?=x, ~?=y)
      THEN @Paste_Over(Mth_False) ENDMATCH;

    IFMATCH Condition !XC dec_less_eq(~?x, ~?y) AND !XC dec_greater(~?=x, ~?=y)
      THEN @Paste_Over(Mth_False) ENDMATCH;

    IFMATCH Condition !XC dec_less_eq(~?x, ~?y) AND !XC dec_less(~?=x, ~?=y)
      THEN @Paste_Over(FILL Condition !XC dec_less(~?x, ~?y) ENDFILL) ENDMATCH;


    IFMATCH Condition NOT (!XC fpx_less(~?x, ~?y)) OR ~?=x = ~?=y
      THEN @Paste_Over(FILL Condition !XC fpx_greater_eq(~?x, ~?y) ENDFILL) ENDMATCH;

    IFMATCH Condition !XC fpx_less(~?x, ~?y) AND ~?=x <> ~?=y
      THEN @Paste_Over(FILL Condition !XC fpx_less(~?x, ~?y) ENDFILL) ENDMATCH;

    IFMATCH Condition NOT (!XC fpx_greater_eq(~?x, ~?y))
      THEN @Paste_Over(FILL Condition !XC fpx_less(~?x, ~?y) ENDFILL) ENDMATCH;


  SKIP OD .;


SKIP


