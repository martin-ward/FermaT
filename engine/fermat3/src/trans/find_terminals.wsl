C:"
==========================================================================
FermaT Transformation System
Copyright (C) 2001 Software Migrations Limited.
Email: martin@gkc.org.uk

This program is free software; you can redistribute it and/or modify
it under the terms of the GNU General Public License as published by
the Free Software Foundation; either version 3 of the License, or
(at your option) any later version.

This program is distributed in the hope that it will be useful,
but WITHOUT ANY WARRANTY; without even the implied warranty of
MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
GNU General Public License for more details.

You should have received a copy of the GNU General Public License
along with this program.  If not, see <http://www.gnu.org/licenses/>.
==========================================================================";
 


MW_PROC @Find_Terminals_Test() ==
   @Pass .;


MW_PROC @Find_Terminals_Code(Data) ==
  IF EMPTY?(Data) OR Data = "" THEN Data := @N_String(@Value(@I)) FI;
  @FT_Process(@Make(T_Comment, "<Terminal: " ++ Data ++ ">", < >), < >) .;


MW_PROC @FT_Process(comment, Procs_Done) ==
  VAR < posn := < > >:
  ATEACH STS DO
    posn := @Posn;
    WHILE @ST(@I) = T_Comment AND @V(@I) <> @V(comment) AND @Left? DO @Left OD;
    IF @ST(@I) = T_Comment AND @V(@I) = @V(comment)
      THEN SKIP
      ELSE @Paste_After(comment);
	   IF @ST(@I) = T_Proc_Call AND @V(@I^1) NOTIN Procs_Done
	     THEN @Goto_Proc_Body(@V(@I^1));
		  IF @ST(@I) = T_Proc
		    THEN @FT_Process(comment, <@V(@I^1)> ++ Procs_Done) FI
	   ELSIF @ST(@I) = T_Cond
	     THEN @FT_Process(comment, Procs_Done) FI FI;
    @Goto(posn) OD ENDVAR .;


C:" Search for the body of the proc with the given name. ";
C:" If not found, then go to the root node. ";

MW_PROC @Goto_Proc_Body(name) ==
  WHILE @Up? AND @ST(@I) <> T_Where DO @Up OD;
  IF @ST(@I) = T_Where
    THEN @Down_To(2); @Down; C:" to first definition ";
	 WHILE @Right? AND (@ST(@I) <> T_Proc OR @V(@I^1) <> name) DO @Right OD;
	 IF @ST(@I) <> T_Proc OR @V(@I^1) <> name
	   THEN C:" move up past the WHERE, if possible, and continue ";
		@Up; @Up; C:" back to the WHERE ";
		IF @Up? THEN @Up; @Goto_Proc_Body(name) FI FI FI .;


SKIP
